plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

repositories {
    mavenCentral()
}

group = 'org.typesense'
version = '0.0.3'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes "Main-Class": "org.typesense.api.Client"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }

}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests (assumes running Docker image or local server)'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

check.dependsOn integrationTest

dependencies {
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "org.glassfish.jersey.core:jersey-client:3.0.4"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:3.0.4"
    implementation "org.glassfish.jersey.inject:jersey-hk2:3.0.4"
    implementation "org.apache.logging.log4j:log4j-api:2.13.3"
    implementation "org.apache.logging.log4j:log4j-core:2.13.3"
    implementation "org.junit.jupiter:junit-jupiter:5.7.0"
    implementation "io.swagger.core.v3:swagger-annotations:2.0.0"
    implementation "org.glassfish.jersey.connectors:jersey-apache-connector:3.0.4"
    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.13.2"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    integrationTestImplementation "junit:junit:4.12"
    integrationTestImplementation "org.hamcrest:hamcrest-all:1.3"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'typesense-java'
                description = 'Typesense Java Client'
                url = 'https://github.com/typesense/typesense-java'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'typesense'
                        name = 'Typesense'
                    }
                }
                scm {
                    url = 'https://github.com/typesense/typesense-java'
                    connection = 'scm:git://github.com/typesense/typesense-java.git'
                    developerConnection = 'scm:git://github.com/typesense/typesense-java.git'
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.mavenJava
    }
}
